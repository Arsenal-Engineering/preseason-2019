import jaci.openrio.gradle.GradleRIOPlugin
import jaci.openrio.gradle.frc.FRCJavaArtifact
import jaci.openrio.gradle.frc.RoboRIO

// Applying plugins to Gradle
plugins {
    // We use Java
    id "java"
    // Allow for the use of the IntelliJ IDEA IDE
    id "idea"
    // Allow for the use of the Eclipse IDE (ew)
    // Depend on GradleRIO
    id "jaci.openrio.gradle.GradleRIO" version "2018.03.06"
    // Depend on SSH plugin for file dropping support
    id 'org.hidetake.ssh' version "2.9.0"
}

// Definitions
def TEAM = 6223
def ROBOT_CLASS = "frc.team6223.robot.Robot"

// Define the script targets (RoboRIO) and artifacts (deployable files)
deploy {
    targets {
        target("roborio", RoboRIO) {
            team = TEAM
        }
    }
    artifacts {
        artifact('frcJava', FRCJavaArtifact) {
            targets << "roborio"
        }
    }
}

// Dependency Repositories
repositories {
    mavenCentral()
}

// Defining dependencies
// Robot: WPILib, CTRE Phoenix (Talon SRX), NavX, Pathfinder
// Kotlin: Kotlin StdLibrary, Kotlin Reflect
// Testing: JUnit 5
dependencies {
    testCompile 'org.assertj:assertj-core:3.9.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.1.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.1.0'

    compile ctre()
    compile wpilib()
    compile navx()
}

// Setting up jar file: generating fat jar, and adding WPILib manifest settings
jar {
    from configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    manifest GradleRIOPlugin.javaManifest(ROBOT_CLASS)
}

test {
    useJUnitPlatform()
}

// SSH tasks and configuration
remotes {
    rio {
        host = 'roboRIO-6223-frc.local'
        user = 'lvuser'
        knownHosts = allowAnyHosts
    }
}

task('copyResources') {
    doLast {
        ssh.run {
            session(remotes.rio) {
                execute 'mkdir ~/6223_resources', ignoreError: true
                execute 'mkdir ~/logs', ignoreError: true
                put from: fileTree('src/main/resources'), into: './6223_resources/'
                execute 'chmod -R 755 ~/6223_resources'
                execute ". /etc/profile.d/natinst-path.sh; /usr/local/frc/bin/frcKillRobot.sh -t -r", ignoreError: true
            }
        }
    }
}

// Setting a task to generate the wrapper files
task wrapper(type: Wrapper) {
    gradleVersion = '4.6'
}